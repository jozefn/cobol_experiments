000010 IDENTIFICATION DIVISION.
000020 PROGRAM-ID. SAMPLE4J.
000030
000040 ENVIRONMENT DIVISION.
000050  CONFIGURATION SECTION.
000060  SPECIAL-NAMES.
000070      CLASS MYALPHA  IS "A" THRU "Z", " "
000080*      CRT STATUS     IS  SCREEN-STATUS
000090      CURSOR         IS      LLCC.
000100  INPUT-OUTPUT SECTION.
000110  FILE-CONTROL.
000120      SELECT OPTIONAL     INDEXED-FILE
000130             ASSIGN       TO  OUTFILE
000140             ORGANIZATION IS  INDEXED
000150             RECORD KEY   IS  PH-NUMBER
000160             ALTERNATE RECORD KEY IS NAME-VALUE  WITH DUPLICATES
000170             FILE STATUS  IS  FILE-STAT
000180             LOCK MODE    IS MANUAL WITH LOCK ON MULTIPLE RECORDS
000190*             LOCK MODE    IS  AUTOMATIC
000200             ACCESS MODE  IS  DYNAMIC.
000210
000220 DATA DIVISION.
000230  FILE SECTION.
000240  FD   INDEXED-FILE.
000250  01   INDEXED-RECORD.
000260       02  PH-NUMBER             PIC 9(4).
000270       02  NAME-VALUE            PIC X(40).
000280
000290  WORKING-STORAGE SECTION.
000300
000310  01   WK-RECORD.
000320       02  WK-PH-NUMBER          PIC 9(4).
000330       02  WK-NAME-VALUE         PIC X(40).
000340
000350
000360  copy screenio.
000370  01  LLCC                       PIC 9(04).
000380  01  OUTFILE                    PIC X(50)
000390      VALUE "/mnt/c/cobfiles\SAMPLE4JOUT.IDX".
000400  01  FILE-STAT                  PIC X(02).
000410  01  ID-NUM-ERROR               PIC X(01) VALUE SPACE.
000420  01  NAME-VALUE-ERROR           PIC X(01) VALUE SPACE.
000430  01  SCREEN-ERROR               PIC 9(01) VALUE ZERO.
000440      88 NO-SCREEN-ERROR-EXIST   VALUE 0.
000450      88 SCREEN-ERROR-EXISTS     VALUE 1.
000460  01  MSG-LINE                   PIC X(80).
000470*  01  EXIT-KEY                   PIC X(85)
000480*       VALUE "F1 = EXIT F4 = LOOKUP ENTER = SAVE "
000490*       & "F7P/F3N = BACK F8P/F2N = FORWARD F9 = DELETE".
000500
000510  01  EXIT-KEY                   PIC X(85)
000520       VALUE "F1 = EXIT F4 = LOOKUP ENTER = SAVE "
000530       & "F2 = NEXT BY NAME F3 = LAST BY NAME".
000540
000550  01  EXIT-KEY2                  PIC X(85)
000560       VALUE "F7 = BACK BY PHONE F8 = NEXT BY PHONE, "
000570       & "F9 = DELETE".
000580
000590  01  SCREEN-STATUS PIC 9(04) .
000600
000610  SCREEN SECTION.
000620  01 CLEAR-SCREEN BLANK SCREEN
000630     BACKGROUND-COLOR IS COB-COLOR-WHITE
000640     FOREGROUND-COLOR IS COB-COLOR-BLACK.
000650  01 MAIN-SCREEN BACKGROUND-COLOR IS COB-COLOR-WHITE
000660                 FOREGROUND-COLOR IS COB-COLOR-BLACK.
000670     05                 LINE 01
000680                        COLUMN 01 VALUE "PH-NUMBER:" HIGHLIGHT.
000690     05 PH-NUMBER-ERROR LINE 01 COLUMN 12
000700        FOREGROUND-COLOR IS COB-COLOR-RED
000710        PIC X(1) FROM ID-NUM-ERROR.
000720     05 PH-NUMBER-AREA  LINE 01 COLUMN 14
000730        FOREGROUND-COLOR IS COB-COLOR-BLUE
000740        PIC 9(4)
000750        FROM PH-NUMBER TO PH-NUMBER AUTO FULL UNDERLINE.
000760     05                 LINE 03 COLUMN 01
000770        VALUE "NAME:     "  HIGHLIGHT.
000780     05 NAME-ERROR      LINE 03 COLUMN 12
000790        FOREGROUND-COLOR IS COB-COLOR-RED
000800        PIC X(1)
000810        FROM NAME-VALUE-ERROR.
000820     05 NAME-AREA       LINE 03 COLUMN 14
000830        FOREGROUND-COLOR IS COB-COLOR-BLUE
000840        PIC X(40)
000850        FROM NAME-VALUE TO NAME-VALUE AUTO UNDERLINE.
000860     05 KEY-DSC-AREA1    LINE 22 COLUMN 01
000870        BACKGROUND-COLOR IS COB-COLOR-BLUE
000880        FOREGROUND-COLOR IS COB-COLOR-WHITE
000890        PIC X(85)
000900        FROM EXIT-KEY.
000910
000920     05 KEY-DSC-AREA2    LINE 23 COLUMN 01
000930        BACKGROUND-COLOR IS COB-COLOR-BLUE
000940        FOREGROUND-COLOR IS COB-COLOR-WHITE
000950        PIC X(85)
000960        FROM EXIT-KEY2.
000970
000980     05 MSG-LINEX       LINE 24 COLUMN 01
000990        FOREGROUND-COLOR IS COB-COLOR-RED
001000        PIC X(77)
001010        FROM MSG-LINE.
001020  01 RING-BELL          LINE 01 COLUMN 01 VALUE " " BELL.
001030
001040 PROCEDURE DIVISION.
001050  MAIN SECTION.
001060  START-PRG.
001070  *> Concuurent user to support file and record locking.
001080      SET ENVIRONMENT "DB_HOME" TO "/mnt/c/cobfiles"
001090      PERFORM OPEN-FILE
001100      DISPLAY CLEAR-SCREEN
001110      PERFORM INITIALIZE-VARIABLES
001120      PERFORM READ-NEXT-RECORD
001130      PERFORM SCREEN-LOOP THRU SCREEN-LOOP-EXIT.
001140
001150  STOP-PRG.
001160      CLOSE INDEXED-FILE.
001170      STOP RUN.
001180
001190  OPEN-FILE.
001200      OPEN I-O INDEXED-FILE
001210      IF FILE-STAT = '00' OR '05'
001220         CONTINUE
001230      ELSE
001240         DISPLAY 'CANNOT OPEN FILE ' FILE-STAT
001250         STOP RUN
001260      END-IF.
001270
001280  INITIALIZE-VARIABLES.
001290      MOVE ZEROS  TO PH-NUMBER
001300      MOVE SPACES TO NAME-VALUE NAME-VALUE-ERROR
001310      MOVE SPACES TO MSG-LINE.
001320
001330  SCREEN-LOOP.
001340      MOVE FUNCTION UPPER-CASE(MSG-LINE) TO MSG-LINE
001350      SET NO-SCREEN-ERROR-EXIST TO TRUE
001360      DISPLAY MAIN-SCREEN
001370      ACCEPT  MAIN-SCREEN
001380      EVALUATE TRUE
001390         WHEN COB-CRT-STATUS = COB-SCR-F1
001400            GO TO SCREEN-LOOP-EXIT
001410         WHEN COB-CRT-STATUS = COB-SCR-F2
001420            PERFORM READ-NEXT-RECORD-BY-NAME
001430         WHEN COB-CRT-STATUS = COB-SCR-F3
001440            PERFORM READ-LAST-RECORD-BY-NAME
001450         WHEN COB-CRT-STATUS = COB-SCR-OK
001460            PERFORM EDIT-SCREEN-FIELDS
001470            IF NO-SCREEN-ERROR-EXIST
001480               MOVE SPACES TO MSG-LINE
001490               PERFORM WRITE-RECORD
001500            END-IF
001510         WHEN COB-CRT-STATUS = COB-SCR-F4
001520            PERFORM READ-RECORD-BY-KEY
001530         WHEN COB-CRT-STATUS = COB-SCR-F7
001540            PERFORM READ-LAST-RECORD
001550         WHEN COB-CRT-STATUS = COB-SCR-F8
001560            PERFORM READ-NEXT-RECORD
001570         WHEN COB-CRT-STATUS = COB-SCR-F9
001580            PERFORM DELETE-RECORD-BY-KEY
001590         WHEN OTHER
001600            MOVE "INVALID RELEASE KEY PRESSED." TO MSG-LINE
001610            DISPLAY RING-BELL
001620      END-EVALUATE
001630      GO TO SCREEN-LOOP.
001640  SCREEN-LOOP-EXIT.
001650      EXIT.
001660
001670  RESET-SCREEN-IND.
001680      MOVE SPACES TO ID-NUM-ERROR NAME-VALUE-ERROR.
001690
001700  UPPER-CASE-FIELDS.
001710      MOVE FUNCTION UPPER-CASE(NAME-VALUE) TO NAME-VALUE.
001720
001730  WRITE-RECORD.
001740      MOVE INDEXED-RECORD TO WK-RECORD
001750      READ INDEXED-FILE WITH LOCK KEY IS PH-NUMBER  *> Lock record before updati
001760      MOVE WK-RECORD TO INDEXED-RECORD
001770
001780      WRITE INDEXED-RECORD
001790      IF FILE-STAT = '00'
001800         MOVE "Record added" to MSG-LINE
001810      ELSE
001820         IF FILE-STAT = '22'
001830            REWRITE INDEXED-RECORD
001840            IF FILE-STAT = '00' OR '02' *> 02 HANDLES DUP ALTERNATE KEY
001850               MOVE "Record updated" TO MSG-LINE
001860            ELSE
001870               STRING "Cannot update record."
001880                      " File-Status = "
001890                      FILE-STAT DELIMITED BY SIZE
001900                      INTO MSG-LINE
001910               END-STRING
001920            END-IF
001930         ELSE
001940            MOVE 'CANNOT WRITE RECORD ' TO MSG-LINE
001950            STOP RUN
001960         END-IF
001970      END-IF.
001980
001990  READ-RECORD-BY-KEY.
002000      PERFORM RESET-SCREEN-IND
002010      MOVE SPACES TO NAME-VALUE
002020      READ INDEXED-FILE KEY IS PH-NUMBER
002030        INVALID KEY
002040          MOVE "KEY NOT FOUND"          TO MSG-LINE
002050          DISPLAY RING-BELL
002060        NOT INVALID KEY
002070          MOVE "RECORD RETRIEVED"       TO MSG-LINE
002080      END-READ
002090      PERFORM RECORD-LOCK-CHECK.
002100
002110  DELETE-RECORD-BY-KEY.
002120      PERFORM RESET-SCREEN-IND
002130      MOVE SPACES TO NAME-VALUE
002140      DELETE INDEXED-FILE RECORD
002150        INVALID KEY
002160          MOVE "KEY NOT FOUND"          TO MSG-LINE
002170          DISPLAY RING-BELL
002180        NOT INVALID KEY
002190          MOVE "RECORD DELETED"         TO MSG-LINE
002200          MOVE SPACES TO NAME-VALUE
002210      END-DELETE.
002220      PERFORM RECORD-LOCK-CHECK.
002230
002240  READ-NEXT-RECORD.
002250      PERFORM RESET-SCREEN-IND
002260      START INDEXED-FILE KEY > PH-NUMBER
002270        INVALID KEY
002280          MOVE "END OF FILE"            TO MSG-LINE
002290          DISPLAY RING-BELL
002300        NOT INVALID KEY
002310          READ INDEXED-FILE NEXT
002320            AT END
002330               MOVE "END OF FILE"       TO MSG-LINE
002340               DISPLAY RING-BELL
002350            NOT AT END
002360               MOVE "RECORD RETRIEVED"  TO MSG-LINE
002370          END-READ
002380          PERFORM RECORD-LOCK-CHECK
002390      END-START.
002400
002410  READ-NEXT-RECORD-BY-NAME.
002420      PERFORM RESET-SCREEN-IND
002430      PERFORM UPPER-CASE-FIELDS
002440      START INDEXED-FILE KEY > NAME-VALUE
002450        INVALID KEY
002460          MOVE "END OF FILE"            TO MSG-LINE
002470          DISPLAY RING-BELL
002480        NOT INVALID KEY
002490          READ INDEXED-FILE NEXT
002500            AT END
002510               MOVE "END OF FILE"       TO MSG-LINE
002520               DISPLAY RING-BELL
002530            NOT AT END
002540               MOVE "RECORD RETRIEVED"  TO MSG-LINE
002550          END-READ
002560          PERFORM RECORD-LOCK-CHECK
002570      END-START.
002580
002590  READ-LAST-RECORD.
002600      PERFORM RESET-SCREEN-IND
002610      START INDEXED-FILE KEY < PH-NUMBER
002620        INVALID KEY
002630          MOVE "BEGINNING OF FILE"      TO MSG-LINE
002640          DISPLAY RING-BELL
002650        NOT INVALID KEY
002660          READ INDEXED-FILE PREVIOUS
002670            AT END
002680               MOVE "BEGINNING OF FILE" TO MSG-LINE
002690               DISPLAY RING-BELL
002700            NOT AT END
002710               MOVE "RECORD RETRIEVED"  TO MSG-LINE
002720          END-READ
002730          PERFORM RECORD-LOCK-CHECK
002740      END-START.
002750
002760  READ-LAST-RECORD-BY-NAME.
002770      PERFORM RESET-SCREEN-IND
002780      PERFORM UPPER-CASE-FIELDS
002790      START INDEXED-FILE KEY < NAME-VALUE
002800        INVALID KEY
002810          MOVE "BEGINNING OF FILE"      TO MSG-LINE
002820          DISPLAY RING-BELL
002830        NOT INVALID KEY
002840          READ INDEXED-FILE PREVIOUS
002850            AT END
002860               MOVE "BEGINNING OF FILE" TO MSG-LINE
002870               DISPLAY RING-BELL
002880            NOT AT END
002890               MOVE "RECORD RETRIEVED"  TO MSG-LINE
002900          END-READ
002910          PERFORM RECORD-LOCK-CHECK
002920      END-START.
002930
002940  RECORD-LOCK-CHECK.
002980     EVALUATE FILE-STAT
002990      WHEN 00 MOVE 'SUCCESS ' TO MSG-LINE
003000      WHEN 02 MOVE 'SUCCESS DUPLICATE ' TO MSG-LINE
003010      WHEN 04 MOVE 'SUCCESS INCOMPLETE ' TO MSG-LINE
003020      WHEN 05 MOVE 'SUCCESS OPTIONAL ' TO MSG-LINE
003030      WHEN 07 MOVE 'SUCCESS NO UNIT ' TO MSG-LINE
003040      WHEN 10 MOVE 'END OF FILE ' TO MSG-LINE
003050      WHEN 14 MOVE 'OUT OF KEY RANGE ' TO MSG-LINE
003060      WHEN 21 MOVE 'KEY INVALID ' TO MSG-LINE
003070      WHEN 22 MOVE 'KEY EXISTS ' TO MSG-LINE
003080      WHEN 23 MOVE 'KEY NOT EXISTS ' TO MSG-LINE
003090      WHEN 30 MOVE 'PERMANENT ERROR ' TO MSG-LINE
003100      WHEN 31 MOVE 'INCONSISTENT FILENAME ' TO MSG-LINE
003110      WHEN 34 MOVE 'BOUNDARY VIOLATION ' TO MSG-LINE
003120      WHEN 35 MOVE 'FILE NOT FOUND ' TO MSG-LINE
003130      WHEN 37 MOVE 'PERMISSION DENIED ' TO MSG-LINE
003140      WHEN 38 MOVE 'CLOSED WITH LOCK ' TO MSG-LINE
003150      WHEN 39 MOVE 'CONFLICT ATTRIBUTE ' TO MSG-LINE
003160      WHEN 41 MOVE 'ALREADY OPEN ' TO MSG-LINE
003170      WHEN 42 MOVE 'NOT OPEN ' TO MSG-LINE
003180      WHEN 43 MOVE 'READ NOT DONE ' TO MSG-LINE
003190      WHEN 44 MOVE 'RECORD OVERFLOW ' TO MSG-LINE
003200      WHEN 46 MOVE 'READ ERROR ' TO MSG-LINE
003210      WHEN 47 MOVE 'INPUT DENIED ' TO MSG-LINE
003220      WHEN 48 MOVE 'OUTPUT DENIED ' TO MSG-LINE
003230      WHEN 49 MOVE 'I/O DENIED ' TO MSG-LINE
003240      WHEN 51 MOVE 'RECORD LOCKED ' TO MSG-LINE
003250      WHEN 52 MOVE 'END-OF-PAGE ' TO MSG-LINE
003260      WHEN 57 MOVE 'I/O LINAGE ' TO MSG-LINE
003270      WHEN 61 MOVE 'FILE SHARING FAILURE ' TO MSG-LINE
003280      WHEN 91 MOVE 'FILE NOT AVAILABLE ' TO MSG-LINE
003290     END-EVALUATE.
003300
003310  EDIT-SCREEN-FIELDS SECTION.
003320******************************************************
003330* Perform edits in reverse field order begining with *
003340* the most common error for a field first.           *
003350* Once a field error is found, set the error flag    *
003360* and go to the next field.                          *
003370******************************************************
003380
003390  RESET-SCREEN-INFO.
003400      MOVE ZEROS  TO LLCC
003410      MOVE SPACES TO MSG-LINE
003420      PERFORM RESET-SCREEN-IND
003430      PERFORM UPPER-CASE-FIELDS.
003440
003450  EDIT-NAME-VALUE.
003460      IF NAME-VALUE = SPACES
003470         MOVE "NAME CANNOT BE BLANK" TO MSG-LINE
003480         SET SCREEN-ERROR-EXISTS TO TRUE
003490         MOVE 0314               TO LLCC
003500         MOVE "*" TO NAME-VALUE-ERROR
003510         DISPLAY RING-BELL
003520         GO TO EDIT-NAME-VALUE-EXIT
003530      END-IF
003540      IF NAME-VALUE NOT MYALPHA
003550         MOVE "NAME MUST CONTAIN ONLY ALPHA CHARACTERS"
003560           TO MSG-LINE
003570         SET SCREEN-ERROR-EXISTS TO TRUE
003580         MOVE 0314               TO LLCC
003590         MOVE "*" TO NAME-VALUE-ERROR
003600         DISPLAY RING-BELL
003610      END-IF.
003620  EDIT-NAME-VALUE-EXIT.
003630      EXIT.
003640
003650  EDIT-ID-FIELD.
003660      IF PH-NUMBER NOT NUMERIC
003670         MOVE "PH-NUMBER MUST BE NUMERIC" TO MSG-LINE
003680         SET SCREEN-ERROR-EXISTS TO TRUE
003690         MOVE 0114               TO LLCC
003700         MOVE "*" TO ID-NUM-ERROR
003710         DISPLAY RING-BELL
003720         GO TO EDIT-ID-FIELD-EXIT
003730      END-IF
003740      IF PH-NUMBER < 1
003750         MOVE "PH-NUMBER MUST BE GREATER THAN ZERO" TO MSG-LINE
003760         SET SCREEN-ERROR-EXISTS TO TRUE
003770         MOVE 0114               TO LLCC
003780         MOVE "*" TO ID-NUM-ERROR
003790         DISPLAY RING-BELL
003800      END-IF.
003810  EDIT-ID-FIELD-EXIT.
003820      EXIT.
003830
003840  END PROGRAM SAMPLE4J.

